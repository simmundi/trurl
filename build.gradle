group 'pl.edu.icm.em'
version '1.3-SNAPSHOT'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://maven.ceon.pl/artifactory/em-releases"
                def snapshotsRepoUrl = "https://maven.ceon.pl/artifactory/em-snapshot"
                url = rootProject.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv("ARTIFACTORY_USR")
                    password System.getenv("ARTIFACTORY_PSW")
                }
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://maven.ceon.pl/artifactory/em-snapshot'
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url 'https://maven.ceon.pl/artifactory/em-releases'
            mavenContent {
                releasesOnly()
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    ext {
        guavaVersion = '30.1-jre'
        bentoVersion = '1.6'
        geotoolsVersion = '26-RC'
        grasshopperVersion = '4.0'
        univocityVersion = '2.8.4'
        fastutilVersion = '8.3.0'
        commonsMathVersion = '3.6.1'
        postgresVersion = '42.2.22'
        roaringBitmapVersion = '0.9.22'
        javaPoetVersion = '1.13.0'
        slf4jVersion = '1.7.30'
        orcVersion = '1.7.0'
        hadoopVersion = '3.3.1'

        junitVersion = '4.12'
        mockitoVersion = '3.11.2'
        juniJupiterApiVersion = '5.7.0'
        assertJVersion = '3.11.1'
    }
}

task clean(type: Delete, group: 'build') {
    delete 'build'
}
