group 'pl.edu.icm.em'
version '1.2-SNAPSHOT'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://maven.ceon.pl/artifactory/em-releases"
                def snapshotsRepoUrl = "https://maven.ceon.pl/artifactory/em-snapshot"
                url = rootProject.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv("ARTIFACTORY_USR")
                    password System.getenv("ARTIFACTORY_PSW")
                }
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://maven.ceon.pl/artifactory/em-snapshot'
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url 'https://maven.ceon.pl/artifactory/em-releases'
            mavenContent {
                releasesOnly()
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    ext {
        guavaVersion = '30.1-jre'
        bentoVersion = '1.4.11'
        geotoolsVersion = '26-RC'
        grasshopperVersion = '4.0'
        univocityVersion = '2.8.4'
        fastutilVersion = '8.3.0'
        commonsMathVersion = '3.6.1'
        postgresVersion = '42.2.22'
        roaringBitmapVersion = '0.9.22'
        javaPoetVersion = '1.9.0'
        slf4jVersion = '1.7.30'
        orcVersion = '1.7.0'
        hadoopVersion = '3.3.1'
        tablesawVersion = '0.38.1'

        junitVersion = '4.12'
        mockitoVersion = '3.11.2'
        juniJupiterApiVersion = '5.7.0'
        assertJVersion = '3.11.1'
    }

    dependencies {
        testAnnotationProcessor project(':trurl-generator')
        testAnnotationProcessor "net.snowyhollows.bento:bento-generator:${bentoVersion}"

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:3.11.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
        testImplementation 'org.assertj:assertj-core:3.11.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
}

task clean(type: Delete, group: 'build') {
    delete 'build'
}
